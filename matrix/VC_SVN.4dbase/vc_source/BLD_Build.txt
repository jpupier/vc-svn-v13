//%attributes = {"lang":"en"} comment added and reserved by 4D.
// --[HEADERSTART]-------------------------------------
// User name (OS): Joshua Fletcher
// Date and time: 05/24/12, 12:45:40
// ----------------------------------------------------
// Description:
//   Build a new component version.
//
//   A "Build" is really a file copy of the interpreted
//   structure and a few other files, plus a simple
//   versioning system.
//
//   The reason the BUILD APPLICATION command is not
//   used (nor the feature) is this must be an
//   interpreted component; 4D's build feature only
//   creates compiled components.
// --[HEADEREND]---------------------------------------
C_LONGINT($currentVersion_l)

C_TEXT($databasePackage_t)
C_TEXT($projectRoot_t)
C_TEXT($componentFolder_t)
C_TEXT($sourceMacros_t)
C_TEXT($sourceResources_t)

If (BLD_CheckForMiscI )

CONFIRM("This will quit 4D in order to complete the build, okay?")

If (OK=1)

$databasePackage_t:=UTIL_FILE_GetDatabaseName +".4dbase"

// Project root is 2 levels up from the database folder.
$projectRoot_t:=UTIL_FILE_GetParent (UTIL_FILE_GetParent (Get 4D folder(Database Folder )))

// This folder contains the component.
$componentFolder_t:=$projectRoot_t+"components"+Folder separator +$databasePackage_t+Folder separator 

// This folder contains necessary macros to run the component.
$sourceMacros_t:=Get 4D folder(Database Folder )+"Macros v2"+Folder separator 

// This folder contains an image for the error dialog.
$sourceResources_t:=Get 4D folder(Current Resources folder )

// The current version is used as a file name.
$currentVersion_l:=BLD_GetVersion 


//===============================================================
// Build actions

// Delete old component
UTIL_FILE_DeleteFolder ($componentFolder_t)

// Mainly seems to be a problem in Windows with the folder taking
// some time to be "really" deleted (i.e. the next call to
// CREATE FOLDER can fail).  Might wrap CREATE FOLDER to trap the
// error and try again if this does not work.
DELAY PROCESS(Current process;10)

// Create new component folder
CREATE FOLDER($componentFolder_t;*)

// Copy Macros V2 folder if it exists
If (Test path name($sourceMacros_t)=Is a folder )
COPY DOCUMENT($sourceMacros_t;$componentFolder_t)
End if 

// Copy Resources folder
COPY DOCUMENT($sourceResources_t;$componentFolder_t)

// Create versioning file
COPY DOCUMENT(Get 4D folder(Current Resources folder )+"version_template.txt";$componentFolder_t+String($currentVersion_l)+".txt")

// Execute a script that will copy files after 4D has quit.
BLD_Finalize ($projectRoot_t)

QUIT 4D
End if 
Else 
ALERT("You cannot build without the MISC I plug-in.")
End if 
