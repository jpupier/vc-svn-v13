//%attributes = {"lang":"en","invisible":true} comment added and reserved by 4D.
//   Moving to OS X I discovered how dismally difficult it is to ensure you have the right version of SVN installed.  I was forced to add a feature to ask the user for the path to 'svn' if necessary. Not wanting to bother the user more than necessary, I persist this in a file located in the host database. This way the component can be upgraded without losing the setting.
C_TEXT($pathToConfig_t;$pattern_t;$configFile_t)
C_LONGINT($start_l;$pos_l;$length_l;$winRef_l)
C_BLOB($configFile_b)

// Form variables.
C_BOOLEAN(VCSVN_CONFIG_Cancelled_f;VCSVN_CONFIG_svnVersionOK_f)
C_TEXT(VCSVN_PathToSVN_t)

$pathToConfig_t:=Get 4D folder(Current Resources folder )+"vc_svn_config.txt"
$pattern_t:="(?<=pathToSVN=).+"

// Look for a saved value.
If (Test path name($pathToConfig_t)=Is a document )

DOCUMENT TO BLOB($pathToConfig_t;$configFile_b)
$configFile_t:=Convert to text($configFile_b;"UTF-8")

$start_l:=1

If (Match regex($pattern_t;$configFile_t;$start_l;$pos_l;$length_l))
<>VCSVN_PathToSVN_t:=Substring($configFile_t;$pos_l;$length_l)
Else 
<>VCSVN_PathToSVN_t:="svn"
End if 

Else 

<>VCSVN_PathToSVN_t:="svn"

End if 

VCSVN_CONFIG_svnVersionOK_f:=VCSVN_UTIL_IsSVNVersionOK (<>VCSVN_PathToSVN_t)

If (VCSVN_CONFIG_svnVersionOK_f=False)
// Prompt the user for valid SVN location.

// Form variable.
VCSVN_PathToSVN_t:=""

$winRef_l:=Open form window("VCSVN_CONFIG_SetSVNPath";Modal form dialog box ;Horizontally Centered ;Vertically Centered )
DIALOG("VCSVN_CONFIG_SetSVNPath")
CLOSE WINDOW($winRef_l)

If (VCSVN_CONFIG_Cancelled_f)
<>VCSVN_PathToSVN_t:=""
End if 
End if 


If (VCSVN_CONFIG_svnVersionOK_f)
$configFile_t:="pathToSVN="+<>VCSVN_PathToSVN_t
CONVERT FROM TEXT($configFile_t;"UTF-8";$configFile_b)
BLOB TO DOCUMENT($pathToConfig_t;$configFile_b)
End if 

$0:=VCSVN_CONFIG_svnVersionOK_f
